plugins {
  id 'application'
  id 'eclipse'
}

sourceCompatibility = 1.7

mainClassName = 'Main'

repositories {
  mavenCentral()
}

processResources {
  into 'build/classes/main'
}

task fetchDependencies(type: Sync) {
    from configurations.runtime
    into "libs"
}

dependencies {
//  compile fileTree(dir: 'libs', include: '*.jar')
  compile 'com.h2database:h2:1.4.187'
  compile 'org.hibernate:hibernate-entitymanager:4.3.6.Final'
  compile 'org.assertj:assertj-core:3.0.0'
  compile 'ch.qos.logback:logback-classic:1.1.3'
  testCompile 'junit:junit:4.11'
}

task startDb(type: JavaExec) {
  main = 'org.h2.tools.Server'
  classpath = sourceSets.main.runtimeClasspath
  args '-tcp', '-tcpDaemon'
}

task stopDb(type: JavaExec) {
  main = 'org.h2.tools.Server'
  classpath = sourceSets.main.runtimeClasspath
  args '-tcpShutdown', 'tcp://localhost:9092'
}

test {
  outputs.upToDateWhen{false}
  jvmArgs '-XX:+UnlockCommercialFeatures',
          '-XX:+FlightRecorder'
}

run {
  jvmArgs '-XX:+UnlockCommercialFeatures',
          '-XX:+FlightRecorder'

  dependsOn startDb
  finalizedBy stopDb
}

